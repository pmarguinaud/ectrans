MODULE INV_TRANS1_SPLIT_MOD
CONTAINS
SUBROUTINE INV_TRANS1_SPLIT (YDGP, YDSP, FSPGL_PROC)

USE PARKIND1          ,ONLY : JPIM, JPRB
USE TPM_TRANS         ,ONLY : LDIVGP, LVORGP
USE TPM_DISTR         ,ONLY : MYPROC, MYSETV
USE INV_TRANS1_CTL_MOD,ONLY : INV_TRANS1_CTL
USE YOMHOOK           ,ONLY : LHOOK, DR_HOOK, JPHOOK
USE POINTER_MOD

IMPLICIT NONE

TYPE (PTRG) :: YDGP (:)
TYPE (PTRS) :: YDSP (:)
EXTERNAL  FSPGL_PROC
OPTIONAL  FSPGL_PROC

TYPE (PTRG) :: YLGP0
TYPE (PTRS) :: YLSP0

TYPE (PTRG), ALLOCATABLE :: YLGP1     (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1VOR  (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1DIV  (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1U    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1V    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1S    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1S_NS (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1S_EW (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1U_EW (:)
TYPE (PTRG), ALLOCATABLE :: YLGP1V_EW (:)

TYPE (PTRS), ALLOCATABLE :: YLSP1     (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1VOR  (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1DIV  (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1U    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1V    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1S    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP1S_NS (:)

TYPE (PTRG), ALLOCATABLE :: YLGP_VOR  (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_DIV  (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_U    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_V    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_S    (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_S_NS (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_S_EW (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_U_EW (:)
TYPE (PTRG), ALLOCATABLE :: YLGP_V_EW (:)

TYPE (PTRS), ALLOCATABLE :: YLSP_VOR  (:)
TYPE (PTRS), ALLOCATABLE :: YLSP_DIV  (:)
TYPE (PTRS), ALLOCATABLE :: YLSP_U    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP_V    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP_S    (:)
TYPE (PTRS), ALLOCATABLE :: YLSP_S_NS (:)

TYPE (PTRG) :: YLGPNULL (0)
TYPE (PTRS) :: YLSPNULL (0)

INTEGER (KIND=JPIM) :: J

REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('INV_TRANS1_SPLIT',0,ZHOOK_HANDLE)

YLGP_VOR  = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_VOR )
YLGP_DIV  = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_DIV )
YLGP_U    = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_U   )
YLGP_V    = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_V   )
YLGP_S    = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_S   )
YLGP_S_NS = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_S_NS)
YLGP_U_EW = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_U_EW)
YLGP_V_EW = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_V_EW)
YLGP_S_EW = PACK (YDGP, MASK=YDGP%ITYPE == NTYPE_S_EW)

YLSP_VOR  = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_VOR )
YLSP_DIV  = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_DIV )
YLSP_U    = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_U   )
YLSP_V    = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_V   )
YLSP_S    = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_S   )
YLSP_S_NS = PACK (YDSP, MASK=YDSP%ITYPE == NTYPE_S_NS)

YLGP1     = YLGPNULL
YLGP1VOR  = YLGPNULL; YLGP1DIV  = YLGPNULL; YLGP1U    = YLGPNULL 
YLGP1V    = YLGPNULL; YLGP1S    = YLGPNULL; YLGP1S_NS = YLGPNULL 
YLGP1U_EW = YLGPNULL; YLGP1V_EW = YLGPNULL; YLGP1S_EW = YLGPNULL

YLSP1     = YLSPNULL
YLSP1VOR  = YLSPNULL; YLSP1DIV  = YLSPNULL; YLSP1U    = YLSPNULL;
YLSP1V    = YLSPNULL; YLSP1S    = YLSPNULL; YLSP1S_NS = YLSPNULL;

J = 0

DO

  DO WHILE (SIZE (YLGP1) < 50)
  
    IF (SIZE (YLGP_U) > 0) THEN
      IF (LVORGP) YLGP1VOR = [YLGP1VOR, SHIFTG (YLGP_VOR)]
      IF (LDIVGP) YLGP1DIV = [YLGP1DIV, SHIFTG (YLGP_DIV)]

      YLGP0 = SHIFTG (YLGP_U)
  
      YLGP1U = [YLGP1U, YLGP0]
      YLGP1V = [YLGP1V, SHIFTG (YLGP_V)]

      IF (YLGP0%IRANK_L > 0) THEN
        YLSP1VOR = [YLSP1VOR, YLSP_VOR (YLGP0%IRANK_L)]
        YLSP1DIV = [YLSP1DIV, YLSP_DIV (YLGP0%IRANK_L)]
        YLSP1U   = [YLSP1U,   YLSP_U   (YLGP0%IRANK_L)]
        YLSP1V   = [YLSP1V,   YLSP_V   (YLGP0%IRANK_L)]
      ENDIF

      IF (SIZE (YLGP_U_EW) > 0) THEN
        YLGP1U_EW = [YLGP1U_EW, SHIFTG (YLGP_U_EW)]
        YLGP1V_EW = [YLGP1V_EW, SHIFTG (YLGP_V_EW)]
      ENDIF
    ENDIF

    IF (SIZE (YLGP_S) > 0) THEN

      YLGP0 = SHIFTG (YLGP_S)
      YLGP1S = [YLGP1S, YLGP0]

      IF (YLGP0%IRANK_L > 0) THEN
        YLSP0 = YDSP (SIZE (YLSP_VOR) + SIZE (YLSP_DIV) + YLGP0%IRANK_L)
        YLSP1S = [YLSP1S, YLSP0]
        IF (YLSP0%IPTR_NS > 0) THEN
          YLSP1S_NS = [YLSP1S_NS, YDSP (YLSP0%IPTR_NS)]
        ENDIF
      ENDIF

      IF (SIZE (YLGP_S_NS) > 0) THEN
        YLGP1S_NS = [YLGP1S_NS, SHIFTG (YLGP_S_NS)]
        YLGP1S_EW = [YLGP1S_EW, SHIFTG (YLGP_S_EW)]
      ENDIF
    ENDIF
    
    YLGP1 = [YLGP1VOR, YLGP1DIV, YLGP1U, YLGP1V, YLGP1S, YLGP1S_NS, YLGP1U_EW, YLGP1V_EW, YLGP1S_EW]
  
    IF ((SIZE (YLGP_U) == 0) .AND. (SIZE (YLGP_S) == 0)) EXIT
  
  ENDDO
  
  IF (SIZE (YLGP1) == 0) EXIT

  YLSP1 = [YLSP1VOR, YLSP1DIV, YLSP1U, YLSP1V, YLSP1S, YLSP1S_NS]

  CALL RENUMG (YLGP1)
  CALL RENUMS (YLSP1)

  CALL INV_TRANS1_CTL (YLGP1, YLSP1, FSPGL_PROC)

  J = J + 1
  WRITE (MYPROC+100, *) __FILE__, ':', __LINE__, J
  CALL PRINTG (YLGP1, MYPROC+100)
  WRITE (MYPROC+100, *) 
  CALL PRINTS (YLSP1, MYPROC+100)
  WRITE (MYPROC+100, *) 
  WRITE (MYPROC+100, *) 
  WRITE (MYPROC+100, *) 
  

  YLSP1 = YLSPNULL

  YLGP1     = YLGPNULL
  YLGP1VOR  = YLGPNULL; YLGP1DIV  = YLGPNULL; YLGP1U    = YLGPNULL 
  YLGP1V    = YLGPNULL; YLGP1S    = YLGPNULL; YLGP1S_NS = YLGPNULL 
  YLGP1U_EW = YLGPNULL; YLGP1V_EW = YLGPNULL; YLGP1S_EW = YLGPNULL

  YLSP1     = YLSPNULL
  YLSP1VOR  = YLSPNULL; YLSP1DIV  = YLSPNULL; YLSP1U    = YLSPNULL;
  YLSP1V    = YLSPNULL; YLSP1S    = YLSPNULL; YLSP1S_NS = YLSPNULL;

ENDDO

IF (LHOOK) CALL DR_HOOK('INV_TRANS1_SPLIT',1,ZHOOK_HANDLE)

CONTAINS

FUNCTION SHIFTG (YDGP)

TYPE (PTRG) :: SHIFTG 
TYPE (PTRG), ALLOCATABLE :: YDGP (:)

IF (SIZE (YDGP) > 0) THEN
  SHIFTG = YDGP (1)
  YDGP = YDGP (2:)
ELSE
  STOP 1
ENDIF

END FUNCTION

END SUBROUTINE INV_TRANS1_SPLIT
END MODULE INV_TRANS1_SPLIT_MOD
