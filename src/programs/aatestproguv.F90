PROGRAM TEST

USE MPL_END_MOD, ONLY : MPL_END
USE MPL_INIT_MOD, ONLY : MPL_INIT
USE PARKIND1, ONLY : JPIM, JPRB
USE MPL_MODULE, ONLY : MPL_BARRIER, MPL_MYRANK, MPL_NPROC
USE MPL_DATA_MODULE, ONLY : MPL_COMM_OML
USE YOMHOOK, ONLY : DR_HOOK, LHOOK
USE SET2PE_MOD, ONLY : SET2PE

USE XRD_GETOPTIONS

IMPLICIT NONE

REAL (KIND=JPRB), PARAMETER :: RPI = 2._JPRB * ASIN (1.0_JPRB)
REAL (KIND=JPRB), PARAMETER :: DEG2RAD = RPI / 180._JPRB

INTEGER(KIND=JPIM) :: NSMAX, NDGLG, NPRGPNS, NPRGPEW, NPRTRW, NPRTRV, NDLON, NPRINTLEV
INTEGER(KIND=JPIM) :: MYPROC, NPROC, NBSETSP
INTEGER(KIND=JPIM) :: NFLEVL
INTEGER(KIND=JPIM) :: NFLEVG
INTEGER(KIND=JPIM) :: NSPEC2, NSPEC2G, NGPTOT, NGPTOTG, MYSETV, MYSETW
INTEGER(KIND=JPIM), ALLOCATABLE :: NLOEN (:) 

REAL (KIND=JPRB) :: ZTSTEP, TIMEF

REAL (KIND=JPRB),    ALLOCATABLE :: ZSPVORG (:,:), ZSPDIVG (:,:), ZSP3AG (:,:)

REAL (KIND=JPRB),    ALLOCATABLE :: ZSPVOR (:,:), ZSPDIV (:,:), ZSP3A (:,:,:)
REAL (KIND=JPRB),    ALLOCATABLE :: ZGPUV (:,:,:,:), ZGP3A (:,:,:,:)
INTEGER (KIND=JPIM), ALLOCATABLE :: NBSETLEV (:), ITO (:)
INTEGER (KIND=JPIM) :: IFLD, ILEVG, IGPG, IPROC, JLEV
INTEGER (KIND=JPIM) :: JSETW, JSETV
INTEGER (KIND=JPIM) :: IA, IB
INTEGER (KIND=JPIM) :: NRGRI (8000)
INTEGER (KIND=JPIM) :: ITIME

LOGICAL :: LUSEFLT, LUSERPNM, LKEEPRPNM
LOGICAL :: LSPLIT, LEQ_REGIONS, LDEBUG
LOGICAL :: LLHOOK

#include "setup_trans0.h"

#include "setup_trans.h"
#include "trans_inq.h"
#include "dist_grid.h"
#include "gath_spec.h"
#include "dir_trans.h"

#include "abor1.intfb.h"

NAMELIST / NAMAATESTPROG / & 
  & NSMAX, NDGLG, NFLEVG, LDEBUG, NDLON, NPRINTLEV

NAMELIST / NAMTRANS / & 
  & LUSEFLT, LUSERPNM, LKEEPRPNM

NAMELIST / NAMPAR0 / & 
  & NPRGPNS, NPRGPEW, NPRTRW, NPRTRV

NAMELIST / NAMPAR1 / & 
  & LEQ_REGIONS, LSPLIT

NAMELIST / NAMRGRI / & 
  & NRGRI

CHARACTER (LEN=256) :: CLNAMELIST, CLFIELDU, CLFIELDV, CLFIELDT
LOGICAL :: LLMPOFF

REAL (KIND=JPRB) :: ZHOOK_HANDLE

CALL INITOPTIONS ()
CLNAMELIST = 'fort.4'
CALL GETOPTION ("--namelist", CLNAMELIST)
CALL GETOPTION ("--lmpoff", LLMPOFF)
CLFIELDU = ''; CALL GETOPTION ("--u-file", CLFIELDU)
CLFIELDV = ''; CALL GETOPTION ("--v-file", CLFIELDV)
CLFIELDV = ''; CALL GETOPTION ("--t-file", CLFIELDT)
CALL CHECKOPTIONS ()


LLHOOK = LHOOK
LHOOK = .FALSE.

OPEN (4, FILE=TRIM (CLNAMELIST), FORM='FORMATTED')

NPRINTLEV = 0
NSMAX     = 0
NDGLG     = 0
NFLEVG    = 19
LDEBUG    = .FALSE.

CALL POSNAM1 (4, 'NAMAATESTPROG')
READ (4, NAMAATESTPROG)

IF ((NSMAX == 0) .OR. (NDGLG == 0)) THEN
  CALL ABOR1 ('DWARF: NSMAX AND NDGLG ARE REQUIRED')
ENDIF

NPRGPNS = 0 
NPRGPEW = 0 
NPRTRW  = 0 
NPRTRV  = 0 

CALL POSNAM1 (4, 'NAMPAR0')
READ (4, NAMPAR0)

LUSEFLT   = NSMAX > 1280
LUSERPNM  = .NOT.LUSEFLT
LKEEPRPNM = .NOT.LUSEFLT

CALL POSNAM1 (4, 'NAMTRANS')
READ (4, NAMTRANS)

LEQ_REGIONS = .TRUE.
LSPLIT      = .TRUE.


CALL POSNAM1 (4, 'NAMPAR1')
READ (4, NAMPAR1)

NRGRI = 0

CALL POSNAM1 (4, 'NAMRGRI')
READ (4, NAMRGRI)

CLOSE (4)

ALLOCATE (NLOEN (NDGLG))
NLOEN (1:NDGLG) = NRGRI (1:NDGLG)

IF (ANY (NLOEN (NDGLG/2+1:NDGLG) == 0)) THEN
  NLOEN (NDGLG/2+1:NDGLG) = NLOEN (NDGLG/2:1:-1)
ENDIF
IF (ANY (NLOEN (NDGLG/2:1:-1) /= NLOEN (NDGLG/2+1:NDGLG))) THEn
  CALL ABOR1 ('UNEXPECTED NLOEN')
ENDIF

IF (LLMPOFF) THEN
  MYPROC = 1
  NPROC  = 1
ELSE
  CALL MPL_INIT 
  MYPROC = MPL_MYRANK()
  NPROC  = MPL_NPROC ()
ENDIF

CALL SQUARE (NPROC, IB, IA)

IF (NPRGPNS == 0 .AND. NPRGPEW == 0) THEN
  NPRGPNS = IA
  NPRGPEW = IB
ENDIF

IF (NPRTRW == 0 .AND. NPRTRV == 0) THEN
  NPRTRW = IA
  NPRTRV = IB
ENDIF

NBSETSP = MIN (NFLEVG+1, NPRTRV)

IF (MYPROC == 1) THEN

  WRITE (0, *) " NPROC = ", NPROC
  WRITE (0, *) " NFLEVG = ", NFLEVG
  WRITE (0, *) " NPRGPNS = ", NPRGPNS, " NPRGPEW = ", NPRGPEW
  WRITE (0, *) " NPRTRW = ", NPRTRW, " NPRTRV = ", NPRTRV
  WRITE (0, *) " LEQ_REGIONS = ", LEQ_REGIONS, " LSPLIT = ", LSPLIT
  WRITE (0, *) " NSMAX = ", NSMAX, " NDGLG = ", NDGLG

ENDIF

MYSETV = MOD (MYPROC-1, NPRTRV) + 1
MYSETW = (MYPROC-1) / NPRTRV + 1

CALL SETUP_TRANS0 (KOUT=0, KERR=0, KPRINTLEV=NPRINTLEV, KMAX_RESOL=2,   &
                 & KPRGPNS=NPRGPNS, KPRGPEW=NPRGPEW, KPRTRW=NPRTRW,     &
                 & LDEQ_REGIONS=LEQ_REGIONS, LDMPOFF=LLMPOFF)

CALL SETUP_TRANS (KSMAX=NSMAX, KDGL=NDGLG, KLOEN=NLOEN, LDSPLIT=LSPLIT, &
                & LDUSEFLT=LUSEFLT,LDUSERPNM=LUSERPNM, LDKEEPRPNM=LKEEPRPNM)

CALL TRANS_INQ (KSPEC2=NSPEC2, KGPTOT=NGPTOT, KSPEC2G=NSPEC2G, KGPTOTG=NGPTOTG)

ALLOCATE (NBSETLEV (NFLEVG))

DO ILEVG = 1, NFLEVG
  NBSETLEV (ILEVG) = 1 + (NPRTRV * (ILEVG - 1)) / NFLEVG
ENDDO

PRINT *, " NBSETLEV = ", NBSETLEV

NFLEVL = COUNT (NBSETLEV == MYSETV)

ALLOCATE (ZSPDIV (NFLEVL, NSPEC2), ZSPVOR (NFLEVL, NSPEC2), ZSP3A (NFLEVL, NSPEC2, 1))
ALLOCATE (ZGPUV (NGPTOT, NFLEVG, 2, 1), ZGP3A (NGPTOT, NFLEVG, 1, 1))

CALL RDFIELD

IF (.NOT. LLMPOFF) CALL MPL_BARRIER()

LHOOK = LLHOOK

IF (LHOOK) CALL DR_HOOK ('AATESTPROG',0,ZHOOK_HANDLE)

CALL DIR_TRANS (PSPDIV=ZSPDIV, PSPVOR=ZSPVOR, PGPUV=ZGPUV, KVSETUV=NBSETLEV, &
              & PSPSC3A=ZSP3A, PGP3A=ZGP3A, KVSETSC3A=NBSETLEV)

IF (.NOT. LLMPOFF) CALL MPL_BARRIER

ALLOCATE (ITO (NFLEVG))

ITO = 1

IF (MYPROC == 1) THEN
  ALLOCATE (ZSPVORG (NFLEVG, NSPEC2G))
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSPVOR, KVSET=NBSETLEV, PSPECG=ZSPVORG)
  OPEN (77, FILE="ZSPVORG.DAT", FORM='FORMATTED')
  DO JLEV = 1, NFLEVG
    WRITE (77, '(I5.5,1000E30.20)') JLEV, ZSPVORG (JLEV, :)
  ENDDO
  CLOSE (77)
  DEALLOCATE (ZSPVORG)
ELSE
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSPVOR, KVSET=NBSETLEV)
ENDIF

IF (MYPROC == 1) THEN
  ALLOCATE (ZSPDIVG (NFLEVG, NSPEC2G))
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSPDIV, KVSET=NBSETLEV, PSPECG=ZSPDIVG)
  OPEN (77, FILE="ZSPDIVG.DAT", FORM='FORMATTED')
  DO JLEV = 1, NFLEVG
    WRITE (77, '(I5.5,1000E30.20)') JLEV, ZSPDIVG (JLEV, :)
  ENDDO
  CLOSE (77)
  DEALLOCATE (ZSPDIVG)
ELSE
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSPDIV, KVSET=NBSETLEV)
ENDIF

IF (MYPROC == 1) THEN
  ALLOCATE (ZSP3AG (NFLEVG, NSPEC2G))
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSP3A (:,:,1), KVSET=NBSETLEV, PSPECG=ZSP3AG)
  OPEN (77, FILE="ZSP3AG.DAT", FORM='FORMATTED')
  DO JLEV = 1, NFLEVG
    WRITE (77, '(I5.5,1000E30.20)') JLEV, ZSP3AG (JLEV, :)
  ENDDO
  CLOSE (77)
  DEALLOCATE (ZSP3AG)
ELSE
  CALL GATH_SPEC (KFGATHG=NFLEVG, KTO=ITO, LDIM1_IS_FLD=.TRUE., PSPEC=ZSP3A (:,:,1), KVSET=NBSETLEV)
ENDIF

IF (LHOOK) CALL DR_HOOK ('AATESTPROG',1,ZHOOK_HANDLE)

IF (.NOT. LLMPOFF) CALL MPL_END

IF (MYPROC == 1) WRITE (0, *) " --------- END --------- "

CONTAINS

SUBROUTINE SQUARE (KN, KA, KB)

INTEGER (KIND=JPIM) :: KN, KA, KB

KB = INT (SQRT (REAL (KN))) + 1

DO
  KA = KN / KB
  IF (KA * KB == KN) EXIT
  KB = KB - 1
ENDDO

END SUBROUTINE SQUARE

SUBROUTINE POSNAM1 (KULNAM,CDNAML)

USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK

IMPLICIT NONE

INTEGER(KIND=JPIM),INTENT(IN)    :: KULNAM 
CHARACTER(LEN=*)  ,INTENT(IN)    :: CDNAML 


#include "abor1.intfb.h"


CHARACTER (LEN = 40) ::  CLINE
CHARACTER (LEN =  1) ::  CLTEST

INTEGER(KIND=JPIM) :: ILEN, IND1, ISTATUS, ISCAN
REAL(KIND=JPRB)    :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('POSNAM',0,ZHOOK_HANDLE)

CLINE='                                        '
REWIND(KULNAM)
ILEN=LEN(CDNAML)
ISTATUS=0
ISCAN=0
DO WHILE (ISTATUS==0 .AND. ISCAN==0)
  READ(KULNAM,'(A)',IOSTAT=ISTATUS) CLINE
  SELECT CASE (ISTATUS)
  CASE (:-1)
    CALL ABOR1 ('POSNAM: CANNOT FIND '//TRIM (CDNAML))
  CASE (0)
    IF (INDEX(CLINE(1:10),'&') == 0) THEN
      ISCAN=0
    ELSE
      IND1=INDEX(CLINE,'&'//CDNAML)
      IF (IND1 == 0) THEN
        ISCAN=0
      ELSE
        CLTEST=CLINE(IND1+ILEN+1:IND1+ILEN+1)
        IF (   (LGE(CLTEST,'0').AND.LLE(CLTEST,'9')) &
         & .OR.(LGE(CLTEST,'A').AND.LLE(CLTEST,'Z')) ) THEN
          ISCAN=0
        ELSE
          ISCAN=1
        ENDIF
      ENDIF
    ENDIF
  CASE (1:)
    CALL ABOR1 ('POSNAM: AN ERROR HAPPENED WHILE READING THE NAMELIST')
  END SELECT
ENDDO
BACKSPACE(KULNAM)

IF (LHOOK) CALL DR_HOOK('POSNAM',1,ZHOOK_HANDLE)
END SUBROUTINE POSNAM1

SUBROUTINE RDFIELD

REAL (KIND=JPRB), ALLOCATABLE :: ZGPBUFG (:, :)
INTEGER (KIND=JPIM) :: IFROM (NFLEVG)
INTEGER (KIND=JPIM) :: JLEV

IFROM = 1

IF (MYPROC == 1) THEN

  ALLOCATE (ZGPBUFG (NGPTOTG, NFLEVG))

  IF (TRIM (CLFIELDU) /= '') THEN

    OPEN (77, FILE=TRIM (CLFIELDU), FORM='FORMATTED', STATUS='OLD')

    DO JLEV = 1, NFLEVG
      READ (77, *) ZGPBUFG (:,JLEV)
    ENDDO

    CLOSE (77)

  ENDIF

  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGPUV (:,:,1,:), PGPG=ZGPBUFG)

  IF (TRIM (CLFIELDV) /= '') THEN

    OPEN (77, FILE=TRIM (CLFIELDV), FORM='FORMATTED', STATUS='OLD')

    DO JLEV = 1, NFLEVG
      READ (77, *) ZGPBUFG (:,JLEV)
    ENDDO

    CLOSE (77)

  ENDIF

  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGPUV (:,:,2,:), PGPG=ZGPBUFG)

  IF (TRIM (CLFIELDT) /= '') THEN

    OPEN (77, FILE=TRIM (CLFIELDT), FORM='FORMATTED', STATUS='OLD')

    DO JLEV = 1, NFLEVG
      READ (77, *) ZGPBUFG (:,JLEV)
    ENDDO

    CLOSE (77)

  ENDIF

  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGP3A (:,:,1,:), PGPG=ZGPBUFG)

  DEALLOCATE (ZGPBUFG)
ELSE
  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGPUV (:,:,1,:))
  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGPUV (:,:,2,:))
  CALL DIST_GRID (KFDISTG=NFLEVG, KFROM=IFROM, PGP=ZGP3A (:,:,1,:))
ENDIF

END SUBROUTINE

END PROGRAM TEST



